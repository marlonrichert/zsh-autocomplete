#!/bin/zsh
zmodload -Fa zsh/files b:zf_mkdir b:zf_rm
zmodload -Fa zsh/parameter p:functions
zmodload -F zsh/zutil b:zstyle
builtin autoload -Uz add-zsh-hook
.autocomplete.mathfunc

.autocomplete.__init__.precmd() {
  emulate -L zsh; setopt $_autocomplete__options

  # Workaround for https://github.com/zdharma/zinit/issues/366
  [[ -v functions[.zinit-shade-off] ]] &&
    .zinit-shade-off "${___mode:-load}"

  local 0=${(%):-%N}
  add-zsh-hook -d precmd $0
  unfunction $0

  # Don't let `&` and `|` remove suffixes.
  [[ -v ZLE_REMOVE_SUFFIX_CHARS ]] ||
    typeset -g ZLE_REMOVE_SUFFIX_CHARS=$' \t\n;'
  [[ -v ZLE_SPACE_SUFFIX_CHARS ]] ||
    typeset -g ZLE_SPACE_SUFFIX_CHARS=$'&|'

  typeset -gH _autocomplete__p=$'\0' _autocomplete__c=$'\n%e %N %x\n'
  typeset -gH _autocomplete__x=$'%(?,,\tâ†’ %?\n)'
  _autocomplete__x+='${${${(%)_autocomplete__p}:#${(%)_autocomplete__c}}:+${_autocomplete__p::=${(%)_autocomplete__c}}}'
  _autocomplete__x+=$'%I\t%(1_,%_ ,)'

  export XDG_DATA_HOME=${XDG_DATA_HOME:-$HOME/.local/share}
  local datadir=$XDG_DATA_HOME/zsh-autocomplete/log
  [[ -d $datadir ]] ||
    zf_mkdir -p $datadir
  local -a older_than_1_day=( $datadir/*(N-.m+1) )
  [[ -n $older_than_1_day ]] &&
    zf_rm -f $older_than_1_day[@]
  exec {_autocomplete__log_fd}>$(mktemp $datadir/XXXXXXXXXXXX)

  export XDG_CACHE_HOME="${XDG_CACHE_HOME:-$HOME/.cache}"
  [[ -d $XDG_CACHE_HOME/zsh ]] ||
    zf_mkdir -pm 0700 $XDG_CACHE_HOME/zsh

  add-zsh-hook precmd .autocomplete.screen-space
  local -i pslines=${#${(%%)PS1}//[^$'\n']}
  typeset -gHi _autocomplete__screen_space=$(( LINES - pslines - 2 ))

  local mod; for mod in compinit config widget key key-binding recent-dirs async; do
    mod=.autocomplete.$mod.precmd
    if [[ -v functions[$mod] ]]; then
      $mod
      unfunction $mod
    fi
  done
}

local mod; for mod in compinit config widget key key-binding recent-dirs async; do
  if ! zstyle -t ':autocomplete:' $mod false no off 0; then
    mod=.autocomplete.$mod
    $mod
    unfunction $mod
    builtin autoload -Uz $mod
  fi
done

add-zsh-hook precmd .autocomplete.__init__.precmd
unfunction .autocomplete.__init__
