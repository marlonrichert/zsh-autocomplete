#!/bin/zsh

.autocomplete.__init__.precmd() {
  emulate zsh; setopt $_autocomplete__options

  # Workaround for https://github.com/zdharma/zinit/issues/366
  [[ -v functions[.zinit-shade-off] ]] &&
    .zinit-shade-off "${___mode:-load}"

  add-zsh-hook -d precmd .autocomplete.__init__.precmd

  # Don't let `|` remove suffixes.
  [[ -v ZLE_REMOVE_SUFFIX_CHARS ]] ||
    export ZLE_REMOVE_SUFFIX_CHARS=$' \t\n;&'

  export XDG_CACHE_HOME="${XDG_CACHE_HOME:-$HOME/.cache}"
  if [[ ! -d $XDG_CACHE_HOME ]]; then
    zmodload -F zsh/files b:zf_mkdir
    zf_mkdir -p $XDG_CACHE_HOME
  fi

  typeset -gHr _autocomplete__sublist_delim=$'[|&!;\n<>{}]'

  autoload -Uz .autocomplete.mathfunc
  .autocomplete.mathfunc
  add-zsh-hook precmd .autocomplete.screen-space

  # Workaround for issue #43
  # https://github.com/marlonrichert/zsh-autocomplete/issues/43
  zle -N zle-line-finish azhw:zle-line-finish
}

add-zsh-hook precmd .autocomplete.__init__.precmd
