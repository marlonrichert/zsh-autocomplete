#!/bin/zsh
zmodload -Fa zsh/files b:zf_mkdir b:zf_rm
builtin autoload -Uz add-zsh-hook .autocomplete.mathfunc
.autocomplete.mathfunc

.autocomplete.__init__.precmd() {
  emulate -L zsh; setopt $_autocomplete__options

  # Workaround for https://github.com/zdharma/zinit/issues/366
  [[ -v functions[.zinit-shade-off] ]] &&
    .zinit-shade-off "${___mode:-load}"

  add-zsh-hook -d precmd ${(%):-%N}
  unfunction ${(%):-%N}

  # Don't let `|` remove suffixes.
  [[ -v ZLE_REMOVE_SUFFIX_CHARS ]] ||
    export ZLE_REMOVE_SUFFIX_CHARS=$' \t\n;&'

  export XDG_CACHE_HOME="${XDG_CACHE_HOME:-$HOME/.cache}"
  if [[ ! -d $XDG_CACHE_HOME ]]; then
    zmodload -F zsh/files b:zf_mkdir
    zf_mkdir -p $XDG_CACHE_HOME
  fi

  add-zsh-hook precmd .autocomplete.screen-space
}

add-zsh-hook precmd .autocomplete.__init__.precmd
