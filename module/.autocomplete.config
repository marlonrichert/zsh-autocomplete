#!/bin/zsh
zmodload -Fa zsh/zutil b:zstyle

.autocomplete.config.precmd() {
  # Remove incompatible settings.
  zstyle -d ':completion:*:functions' ignored-patterns
  zstyle -d ':completion:*:*:*:*:*' menu
  zstyle -d '*' single-ignored
  zstyle -d ':completion:*' special-dirs

  local -a completers=( _expand _complete _autocomplete.history_words _correct _ignored )
  zstyle ':completion:*' completer _autocomplete.oldlist $completers[@]
  zstyle ':completion:history-incremental-*search-*:*' completer _autocomplete.history_lines
  zstyle ':completion:list-choices:*' completer $completers[@]
  zstyle ':completion:list-expand:*' completer $completers[@]

  # Order of specs in a matcher matters. If we put l:?|=[[:punct:]] before r:|[[:punct:]]=**, the
  # matching does not work as expected.
  zstyle ':completion:*:complete:*' matcher-list \
    'r:|[[:punct:]]=** l:?|=[[:punct:]] m:{[:lower:][:punct:]}={[:upper:][:punct:]}' \
    'r:|?=** m:{[:lower:]}={[:upper:]}'
  zstyle ':completion:list-expand:complete:*' matcher 'l:|=**'

  zstyle ':completion:*:(functions|parameters|users)' ignored-patterns '[[:punct:]]*[[:alnum:]]*'
  zstyle ':completion:*:(alias-expansions|history-(lines|words)|requoted|unambiguous)' \
    ignore-line current
  zstyle ':completion:*' ignore-parents 'parent pwd directory'

  zstyle ':completion:*' sort yes
  zstyle ':completion:*:(executables|*directories|*files)' sort no
  zstyle ':completion:*:(options|values)' sort no

  zstyle ':completion:*' group-name ''

  zstyle ':completion:*' group-order \
    options arguments values \
    local-directories executables suffix-aliases files \
    reserved-words aliases functions commands builtins jobs \
    ancestor-directories named-directories recent-directories path-directories directories \
    recent-files parameters
  zstyle -e ':completion:*:complete:*' tag-order '
    reply=( ''! options *files *directories'' ''*files *directories'' - )
    [[ $PREFIX$SUFFIX == [-+]* && $compstate[context] == (command|condition) ]] &&
      reply=( ''(|*-)argument-* (|*-)option[-+]* values options'' - )
  '
  zstyle -e ':completion:*:complete:-command-:*' tag-order '
    reply=( ''*'' )
    [[ -z $BUFFER ]] &&
      reply=( ''executables local-directories suffix-aliases'' - )
  '
  zstyle ':completion:*:(approximate|correct):*' tag-order '! original' -
  zstyle ':completion:*:expand:*' tag-order '! all-expansions original' -

  _autocomplete__lowercase() { REPLY=${(L)REPLY} }
  zstyle ':completion:*' file-patterns '
    *(D-/o+_autocomplete__lowercase):local-directories:"local directory"
    *(D-^/O+_autocomplete__lowercase):files:file
  '
  zstyle ':completion:*:-command-:*' file-patterns '
    *(D-*O+_autocomplete__lowercase):executables:"executable file"
    *(D-/o+_autocomplete__lowercase):local-directories:"local directory"
  '

  zstyle -e ':completion:*' glob 'reply=( "true" ); _autocomplete.is_glob || reply=( "false" )'
  zstyle ':completion:*' expand suffix
  zstyle ':completion:*' keep-prefix false
  zstyle ':completion:*' prefix-needed false
  zstyle ':completion:*' list-suffixes true
  zstyle ':completion:*' accept-exact-dirs true
  zstyle ':completion:*' path-completion true

  zstyle -d ':completion:*:default' list-prompt
  unset LISTPROMPT
  zstyle ':completion:*:default' menu 'yes' 'select'
  zstyle ':completion:*' select-prompt ''
  zstyle ':completion:*' select-scroll 0

  zstyle ':completion:*:history-lines' format ''

  local h1=$'%{\e[01;02;39m%}' end=$'%{\e[0m%}' hint=$'%{\e[22;02;39m%}' kbd=$'%{\e[22;39m%}'
  zstyle ':completion:*:descriptions' format "$h1%d$end"
  zstyle ':completion:*:messages' format "$h1%d$end"
  zstyle -e ':completion:*:warnings' format '
    local d=${${(j:, :)_lastdescr[@]:#}/(#m)*, /$MATCH[1,-3] or }
    reply=( "'$h1'No ${tail:+matching }$d completions found.'$end'" )'
  zstyle ':completion:*:(alias-expansions|requoted|unambiguous)' format \
    "$h1%d$hint  (press ${kbd}Shift${hint}+${kbd}Tab$hint to insert)$end"
  zstyle ':completion:*' auto-description '%d'

  zstyle ':completion:*' add-space yes
  zstyle ':completion:*' max-matches-width $COLUMNS
  zstyle ':completion:*' use-cache yes
  zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zcompcache"

  zstyle ':completion:*' insert-sections yes
  zstyle ':completion:*' separate-sections yes
}
