#!/bin/zsh

.autocomplete.config.precmd() {
  emulate -L zsh -o extendedglob -o rcquotes
  add-zsh-hook -d precmd .autocomplete.config.precmd

  zmodload zsh/zutil  # `zstyle` builtin

  if (( ${#$(zstyle -L ':autocomplete:list-choices:*')} > 0 )); then
    print -u2 \
      'Warning: `zstyle '':autocomplete:list-choices:*''` settings are no longer supported.'
    print -u2 'Please use `zstyle '':autocomplete:*''` for these settings instead.'
  fi

  # Remove incompatible settings.
  zstyle -d ':completion:*:functions' ignored-patterns
  zstyle -d ':completion:*:*:*:*:*' menu
  zstyle -d '*' single-ignored
  zstyle -d ':completion:*' special-dirs

  local -a completers=( _expand _complete _correct _ignored _autocomplete.history_words )
  zstyle ':completion:*' completer _autocomplete.oldlist $completers[@]
  zstyle ':completion:list-expand:*' completer $completers[@]

  zstyle ':completion:*:complete:*' matcher-list 'm:{[:upper:][:lower:]-_}={[:lower:][:upper:]_-}'
  zstyle ':completion:list-expand:complete:*' \
    matcher-list 'l:|=** m:{[:upper:][:lower:]-_}={[:lower:][:upper:]_-}'
  zstyle ':completion:*' matcher 'r:|?=**'
  zstyle ':completion:*:*:options' matcher 'r:|?=** m:{-+}={+-}'
  zstyle ':completion:*:-command-:*' matcher 'r:|=*'
  zstyle ':completion:*:(-command-:*|*:(commands|parameters|users))' \
    ignored-patterns '(*/)#[^[:alnum:]](*[[:alnum:]]*~*/*)'
  zstyle ':completion:*:*:(executables|*directories)' ignored-patterns ''
  zstyle ':completion:*:*:history-(lines|words)' ignored-patterns ''
  zstyle ':completion:*:*:unambiguous' ignored-patterns ''
  zstyle ':completion:*:(alias-expansions|history-(lines|words)|requoted|unambiguous)' \
    ignore-line current
  zstyle ':completion:*' ignore-parents 'parent pwd directory'

  zstyle ':completion:*' sort yes
  zstyle ':completion:*:((|*-)argument-*|(|*-)option[-+]*|values|options)' sort no

  local h1=$'%{\e[01;02;39m%}' end=$'%{\e[0m%}' hint=$'%{\e[22;02;39m%}' kbd=$'%{\e[22;39m%}'

  zstyle ':completion:*' group-name ''
  zstyle ':completion:*:-command-:*' group-name commands
  zstyle ':completion:*:-command-:*' format "${h1}command$end"
  zstyle ':completion:*:-command-:*:(*directories|executables|parameters|reserved-words)' \
    group-name ''
  zstyle ':completion:*:-command-:*:(*directories|executables|parameters|reserved-words)' \
    format "$h1%d$end"

  zstyle ':completion:*' group-order \
    options arguments values \
    executables local-directories files \
    commands path-directories recent-directories recent-files
  zstyle -e ':completion:*' tag-order '
    if [[ $compstate[context] == (command|condition) ]]; then
      if [[ $PREFIX$SUFFIX == [-+]* ]]; then
        reply=( "(|*-)argument-* (|*-)option[-+]* values" options - )
      else
        reply=( "! options *files *directories" "*files *directories" - )
      fi
    else
      reply=( "*" )
    fi
  '
  zstyle ':completion:*:(approximate|correct):*' tag-order '! original' -
  zstyle ':completion:*:expand:*' tag-order '! all-expansions original' -

  zstyle ':completion:*' file-patterns '
    *(D-*):executables:"executable file"
    *(D-/):local-directories:"local directory"
    *(D-^/*):files:file
  '
  zstyle ':completion:*:-command-:*' file-patterns '
    *(D-*):executables:"executable file"
    *(D-/):local-directories:"local directory"
  '

  zstyle -e ':completion:*' glob 'reply=( "true" ); _autocomplete.is_glob || reply=( "false" )'
  zstyle ':completion:*' expand suffix
  zstyle ':completion:*' keep-prefix false
  zstyle ':completion:*' prefix-needed false
  zstyle ':completion:*' list-suffixes true
  zstyle ':completion:*' accept-exact-dirs true
  zstyle ':completion:*' path-completion true

  zstyle -d ':completion:*:default' list-prompt ''
  unset LISTPROMPT
  zstyle ':completion:*:default' menu 'yes' 'select'
  zstyle ':completion:list-choices:*:default' menu 'no' 'no-select'
  zstyle ':completion:*:default' select-prompt ''
  zstyle ':completion:*:default' select-scroll 0

  zstyle ':completion:history-search:*:history-lines' format ''

  local null
  if zstyle -s :autocomplete:tab: completion null; then
    print -u2 'Warning: `zstyle '':autocomplete:*'' groups always` is no longer supported.'
    print -u2 'Please use `zstyle '':completion:*'' group-name ''''` instead.'
  fi

  zstyle ':completion:*' list-rows-first 'yes'

  zstyle ':completion:*:descriptions' format "$h1%d$end"
  zstyle ':completion:*:messages' format "$h1%d$end"
  zstyle -e ':completion:*:warnings' format '
    local d=${${(j:, :)_lastdescr[@]:#}/(#m)*, /$MATCH[1,-3] or }
    reply=( "'$h1'No ${tail:+matching }$d completions found.'$end'" )'
  zstyle ':completion:*' auto-description '%d'

  local backtab="press ${kbd}Shift${hint}+${kbd}Tab$hint to insert"
  zstyle ':completion:*:(alias-expansions|requoted|unambiguous)' format \
    "$h1%d$hint  ($backtab)$end"
  zstyle ':completion:*:history-lines' format \
    "$h1%d$hint  ($backtab; press ${kbd}â†‘$hint or ${kbd}PgUp$hint for more)$end"

  zstyle ':completion:*' add-space yes
  zstyle ':completion:*:default' list-packed yes
  zstyle ':completion:*' max-matches-width $COLUMNS
  zstyle ':completion:*' use-cache yes
  zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zcompcache"
}

add-zsh-hook precmd .autocomplete.config.precmd
