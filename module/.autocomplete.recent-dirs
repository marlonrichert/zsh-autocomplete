#!/bin/zsh
zmodload -Fa zsh/parameter p:commands p:functions

.autocomplete.recent-dirs.precmd() {
  if [[ -v functions[zshz] && -v functions[_zshz_precmd] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'zsh-z'; then

    _autocomplete.recent_dirs() {
      reply=( "$( zshz --complete -l $1 2>/dev/null )" )
    }

  elif [[ -v commands[zoxide] && -v functions[__zoxide_hook] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'zoxide'; then

    _autocomplete.recent_dirs() {
      reply=( "$( zoxide query --list $1 2>/dev/null )" )
    }

  elif [[ -v functions[_zlua] && -v functions[_zlua_precmd] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'z.lua'; then

    _autocomplete.recent_dirs() {
      reply=( "${(@)${(f)$( _zlua --complete $1 2>/dev/null )}##<->[[:space:]]##}" )
    }

  elif [[ -v functions[_z] && -v functions[_z_precmd] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'z.sh'; then

    _autocomplete.recent_dirs() {
      reply=( "${(@)${(f)$( _z -l $1 2>&1 )}##(common:|<->)[[:space:]]##}" )
    }

  elif [[ -v commands[autojump] && -v AUTOJUMP_SOURCED ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'autojump'; then

    _autocomplete.recent_dirs() {
      reply=( "${(@)${(f)$( autojump --complete $1 2>/dev/null )}##${1}__<->__}" )
    }

  elif [[ ( -v commands[fasd] || -v functions[fasd] ) && -v functions[_fasd_preexec] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'fasd'; then

    _autocomplete.recent_dirs() {
      reply=( "$( fasd -dlR $1 2>/dev/null )" )
    }

  else
    builtin autoload -Uz chpwd_recent_dirs chpwd_recent_filehandler
    add-zsh-hook chpwd chpwd_recent_dirs
    zstyle -s :chpwd: recent-dirs-max _ ||
      zstyle ':chpwd:*' recent-dirs-max 0
    zstyle -s :chpwd: recent-dirs-prune _ ||
      zstyle ':chpwd:*' recent-dirs-prune "pattern:^${HOME//[\/]/\\/}$" \
        "pattern:^${HOME//[\/]/\\/}\/[^\/]+$" 'pattern:^\/[^\/]+$' "pattern:^${TMPDIR:A}/.*$"

    .autocomplete.recent_dirs.chpwd() {
      chpwd_recent_filehandler
      typeset -gHa _autocomplete__recent_dirs=( $~reply[@] )
    }
    add-zsh-hook chpwd .autocomplete.recent_dirs.chpwd
    .autocomplete.recent_dirs.chpwd

    _autocomplete.recent_dirs() {
      reply=( $_autocomplete__recent_dirs[@] )
      local ancestor=$PWD:h
      while [[ $ancestor != '/' ]]; do
        reply=( ${(@)reply[@]:#$ancestor} )
        ancestor=$ancestor:h
      done
      reply=( ${(@)reply[@]:#$PWD(|/[^/]#)} )
      reply=( ${(M@)reply[@]:#**/(#i)${~${:-$PREFIX$SUFFIX}///[^/]#}[^/]#} )
      reply=( ${(@)reply[1,10]} )
      (( $#reply > 0 ))
    }
  fi

  if [[ ( -v commands[fasd] || -v functions[fasd] ) && -v functions[_fasd_preexec] ]] &&
      zstyle -T ':autocomplete:' recent-files 'fasd'; then

    _autocomplete.recent_files() {
      reply=( "$( fasd -flR $1 2>/dev/null )" )
    }
  fi
}
