#!/bin/zsh
zmodload zsh/complist
zmodload -F zsh/parameter p:funcstack p:functions
zmodload -F zsh/terminfo p:terminfo

.autocomplete.key-binding.precmd() {
  emulate -L zsh
  setopt $_autocomplete__func_opts[@]

  .autocomplete.key-binding.rebind main  expand-or-complete        complete-word '\t'
  .autocomplete.key-binding.rebind main  expand-or-complete-prefix complete-word '\t'
  .autocomplete.key-binding.rebind main  menu-expand-or-complete   complete-word '\t'

  .autocomplete.key-binding.bound main '\t' complete-word &&
      .autocomplete.key-binding.bind-menu accept-line '\t'

  .autocomplete.key-binding.bind   main  history-search-backward $_autocomplete__key_alt_up[@]
  .autocomplete.key-binding.bind   main  menu-select             $_autocomplete__key_alt_down[@]

  .autocomplete.key-binding.rebind emacs history-search-backward history-search-backward '\ep'
  .autocomplete.key-binding.rebind emacs history-search-forward  menu-select             '\en'

  .autocomplete.key-binding.rebind vicmd vi-rev-repeat-search    history-search-backward 'N'
  .autocomplete.key-binding.rebind vicmd vi-repeat-search        menu-select             'n'

  .autocomplete.key-binding.rebind emacs history-incremental-search-backward{,}         '^R'
  .autocomplete.key-binding.rebind emacs history-incremental-search-forward menu-search '^S'

  .autocomplete.key-binding.rebind vicmd {vi-history,history-incremental}-search-backward '/'
  .autocomplete.key-binding.rebind vicmd  vi-history-search-forward         menu-search   '?'

  unset -m '_autocomplete__key_*'
  unfunction .autocomplete.key-binding.{{,re,un}bind,bound}
}

.autocomplete.key-binding.bind() {
  local -P keymap=$1 widget=$2
  shift 2
  builtin bindkey -M "$keymap" "${@:^^widget}"
}

.autocomplete.key-binding.bind-menu() {
  .autocomplete.key-binding.bind isearch "$@"
  .autocomplete.key-binding.bind menuselect "$@"
}

.autocomplete.key-binding.bound() {
  [[ $( builtin bindkey -M "$1" "$2" ) == \"[^[:space:]]##\"" $3" ]]
}

.autocomplete.key-binding.rebind() {
  local -P keymap=$1 old=$2 new=$3 key=
  shift 3
  for key; do
    .autocomplete.key-binding.bound "$keymap" "$key" "$old" &&
        builtin bindkey -M "$keymap" "$key" "$new"
  done
}

.autocomplete.key-binding.unbind() {
  .autocomplete.key-binding.bound "$1" "$2" "$3" &&
      builtin bindkey -M "$1" -r "$2"
}


local -Pa prefix=( '\e'{\[,O} )
typeset -ga _autocomplete__key_up=( ${^prefix}A )
typeset -ga _autocomplete__key_down=( ${^prefix}B )
typeset -ga _autocomplete__key_alt_up=( '\e'$^_autocomplete__key_up '\e[1;3A' )
typeset -ga _autocomplete__key_alt_down=( '\e'$^_autocomplete__key_down '\e[1;3B' )

.autocomplete.key-binding.bind   main    up-line-or-search $_autocomplete__key_up[@]
.autocomplete.key-binding.bind   main  down-line-or-select $_autocomplete__key_down[@]

.autocomplete.key-binding.rebind emacs   up-line-or-{history,search} '^P'
.autocomplete.key-binding.rebind emacs down-line-or-{history,select} '^N'

.autocomplete.key-binding.rebind vicmd   up-line-or-{history,search} 'k'
.autocomplete.key-binding.rebind vicmd down-line-or-{history,select} 'j'

local -Pa menukeys=(
                '^@' accept-and-hold
                '^_' .undo
    "$terminfo[kpp]" backward-word
    "$terminfo[knp]"  forward-word
               '^[v' backward-word
                '^V'  forward-word
                '^R' history-incremental-search-backward
                '^S' history-incremental-search-forward
)
builtin bindkey -M isearch    "$menukeys[@]"
builtin bindkey -M menuselect "$menukeys[@]"

.autocomplete.key-binding.bind-menu vi-backward-blank-word $_autocomplete__key_alt_up[@]
.autocomplete.key-binding.bind-menu vi-forward-blank-word  $_autocomplete__key_alt_down[@]
