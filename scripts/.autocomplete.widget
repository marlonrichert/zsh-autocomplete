#!/bin/zsh
typeset -g ZSH_AUTOSUGGEST_MANUAL_REBIND=1
zmodload zsh/complist

.autocomplete.widget.c() {
  zle -C $1 $2 .autocomplete.$3.completion-widget
}

.autocomplete.widget.z() {
  zle -N $1 .autocomplete.$2.zle-widget
}

.autocomplete.widget.non-tab() {
  .autocomplete.widget.c list-expand complete-word list-expand
  .autocomplete.widget.c _history_search menu-select history-search
  .autocomplete.widget.z history-search history-search
  .autocomplete.widget.z up-line-or-search up-line-or-search
  .autocomplete.widget.z down-line-or-select down-line-or-select
}

.autocomplete.widget.precmd() {
  local tab_style; for tab_style in complete-word menu-complete menu-select; do
    .autocomplete.widget.c $tab_style $tab_style complete-word
  done
  .autocomplete.widget.c reverse-menu-complete reverse-menu-complete complete-word

  # Declare these again (see below), because compinit overrides some of them.
  .autocomplete.widget.non-tab

  unfunction .autocomplete.widget.c .autocomplete.widget.z
}

# Declare these already, so syntax highlighting doesn't complain. See above.
.autocomplete.widget.non-tab
