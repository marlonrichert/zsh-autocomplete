#autoload
zmodload -F zsh/parameter p:functions

_autocomplete.recent_paths() {
  (( _matcher_num > 1 )) &&
    return 1

  local -aU reply=()
  local tag
  _tags recent-directories recent-files
  while _tags; do

    tag=recent-directories
    _requested $tag &&
      [[ -v functions[_autocomplete.recent_dirs] && $_comp_tags == *( | local-)directories* ]] &&
      _autocomplete.recent_dirs "$PREFIX$SUFFIX" &&
      .autocomplete.recent_paths.add $tag 'recent directory' $reply[@]

    tag=recent-files
    _requested $tag &&
      [[ -v functions[_autocomplete.recent_files] && $_comp_tags == *( | all-)'files '* ]] &&
      _autocomplete.recent_files "$PREFIX$SUFFIX" &&
      .autocomplete.recent_paths.add $tag 'recent file' $reply[@]
  done

  return 1  # Always continue to next completer.
}

.autocomplete.recent_paths.add() {
  local -a displ expl line
  _description -V $1 expl $2
  shift 2

  # Work around `setopt autonamedirs` by not assigning absolute path to scalar.
  while (( # )); do
    displ=( "${(D)1}" )
    if [[ $displ == ??* ]]; then

      # Workaround to prevent the prompt from jumping
      line+=( "$displ/" )
      (( ${#$( print -c $line[@] )} < COLUMNS )) ||
        break

      compadd "$expl[@]" -d displ -P "${${(D)1:h}%/}/" -fW "${${1:h}%/}/" - "$1:t"
    fi
    shift
  done
}

_autocomplete.recent_paths "$@"
