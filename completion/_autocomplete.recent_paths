#autoload
zmodload -F zsh/parameter p:functions

_autocomplete.recent_paths() {
  (( _matcher_num > 1 )) &&
    return 1

  local -aU reply=()
  local tag
  _tags recent-directories recent-files
  while _tags; do

    tag=recent-directories
    _requested $tag &&
      [[ -v functions[_autocomplete.recent_dirs] && $_comp_tags == *( | local-)directories* ]] &&
      _autocomplete.recent_dirs "$PREFIX$SUFFIX" &&
      .autocomplete.recent_paths.add $tag 'recent directory' $reply[@]

    tag=recent-files
    _requested $tag &&
      [[ -v functions[_autocomplete.recent_files] && $_comp_tags == *' files '* ]] &&
      _autocomplete.recent_files "$PREFIX$SUFFIX" &&
      .autocomplete.recent_paths.add $tag 'recent file' $reply[@]
  done

  return 1  # Always continue to next completer.
}

.autocomplete.recent_paths.add() {
  local expl _comp_no_ignore=yes _saved_list='list rows'
  _description -V $1 expl $2
  shift 2
  local -a displ=( "${(D)@}" )
  compadd "$expl[@]" -d displ -fW / -U - "$@"
}

_autocomplete.recent_paths "$@"
