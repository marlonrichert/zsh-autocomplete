#autoload

(( compstate[nmatches] > 1 )) ||
  return 1
[[ -z $BUFFER || $curcontext == history-*:* ||
    $IPREFIX$PREFIX$SUFFIX$ISUFFIX == *${compstate[unambiguous]:#?}* ]] &&
  return 1

if zstyle -t :autocomplete:tab: insert-unambiguous; then
  local subs cols
  local -i prv=1 nxt; for nxt in ${(s.:.)compstate[unambiguous_positions]}; do
    subs+='('${(b)compstate[unambiguous][prv,nxt]}')*'
    cols+='=30;103'  # black foreground, bright yellow background
    (( prv = ++nxt ))
  done
  ZLS_COLORS="=(#bi)*$subs=$cols:$ZLS_COLORS"
  return 1
fi

local tag=unambiguous
_tags $tag
_tags && _requested $tag &&
  _message -e unambiguous "common substring:%b %F{0}%K{11}$compstate[unambiguous]%f%k"
  # Black text on bright yellow background
