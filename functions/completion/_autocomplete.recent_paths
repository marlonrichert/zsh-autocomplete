#autoload
zmodload -F zsh/parameter p:functions

_autocomplete.recent_paths() {
  (( compstate[nmatches] > 1 )) &&
      return 1

  private -aU dirs=()
  private -i ret=1
  private tag=
  _tags recent-directories recent-files
  while _tags; do

    tag=recent-directories
    if _requested $tag &&
        [[ -v functions[_autocomplete.recent_dirs] && $_comp_tags == *( | local-)directories* ]] &&
        _autocomplete.recent_dirs "$PREFIX$SUFFIX"; then
      dirs=( "$reply[@]" )
      while _next_label -V $tag expl 'recent directory'; do
        .autocomplete.recent_paths.add $dirs[@] &&
            ret=0
      done
    fi

    tag=recent-files
    if _requested $tag &&
        [[ -v functions[_autocomplete.recent_files] && $_comp_tags == *( | all-)'files '* ]] &&
        _autocomplete.recent_files "$PREFIX$SUFFIX"; then
      dirs=( "$reply[@]" )
      while _next_label -v $tag expl 'recent file'; do
        .autocomplete.recent_paths.add $dirs[@] &&
            ret=0
      done
    fi
  done

  return ret
}

.autocomplete.recent_paths.add() {
  local -a displ=() matches=( "$@" )
  builtin compadd "$expl[@]" -D matches -- "$@:t"
  displ=( "${(@D)matches}" )
  zformat -a displ '' $^displ':'

  .autocomplete.recent_paths.trim

  private -i ret=1

  # Work around `setopt autonamedirs` by not assigning absolute paths to scalars.
  while (( $#matches )); do
    (( $#displ[1] > 1 )) &&
        compadd "$expl[@]" -d displ -P "${${(D)matches[1]:h}%/}/" -fW "${${matches[1]:h}%/}/" \
              -- "$matches[1]:t" &&
            ret=0
    shift -- displ matches
  done

  return ret
}

# Workaround to prevent the prompt from jumping
.autocomplete.recent_paths.trim() {
  private total="${(j:/  :)displ}/  "
  while (( $#total > COLUMNS )); do
    shift -p -- displ matches
    total="${(j:/  :)displ}/  "
  done
}

_autocomplete.recent_paths "$@"
