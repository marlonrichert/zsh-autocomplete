#autoload
local -a match=() mbegin=() mend=()  # `zstyle` for some reason assigns these.

unset MENUMODE MENUSELECT
compstate[insert]=
compstate[list]=
compstate[restore]=
compstate[to_end]=${${_completer:/prefix/}:+always}

if [[ -n $_autocomplete__unambiguous ]] &&
    builtin zstyle -t ":autocomplete:$curcontext" insert-unambiguous; then

  [[ $WIDGETSTYLE == (|*-)menu(|-*) ]] &&
      compstate[insert]='automenu-'

  compstate[insert]+='unambiguous'

  unset _autocomplete__unambiguous

  return  # !
fi

local -Pa comptags=()
local -Pi nmatches=0

if [[ $compstate[old_list] == keep ]]; then
  comptags=( $=_lastcomp[tags] )
  nmatches=$_lastcomp[nmatches]
else
  comptags=( $=_comp_tags )
  nmatches=$compstate[nmatches]
fi

if (( nmatches > 1 )); then
  [[ $WIDGETSTYLE == (|*-)select(|-*) ]] &&
      typeset -gHi MENUSELECT=0
  [[ $WIDGETSTYLE == (|*-)menu(|-*) ]] &&
      compstate[insert]='menu:'

  # Determine which terminal line we'll be on (for async completion).
  typeset -gHi _autocomplete__buffer_start_line=$((
      max(
          BUFFERLINES,
          min(
              _autocomplete__buffer_start_line,
              LINES - compstate[list_lines]
          )
      )
  ))
fi

if [[ $WIDGET == (|.)reverse-* || $WIDGETSTYLE == (|.)reverse-menu-complete ]]; then
  compstate[insert]+='0'
else
  compstate[insert]+='1'
fi

local -a spacetags=()
builtin zstyle -a :autocomplete: add-space spacetags ||
    spacetags=( executables aliases functions builtins reserved-words commands )
[[ -n ${comptags:*spacetags} ]] &&
    compstate[insert]+=' '
