#!/bin/zsh
local 0=${(%):-%N}
zle -C ${0}.context list-choices ${0}.context

${0}.context() {
  # Inside a completion widget, the current quote level is removed. So, we should not include
  # $QIPREFIX/$QISUFFIX in our pattern or it won't match.
  lcontext=${LBUFFER%${(~j:*:)words[1,CURRENT-1]}*$IPREFIX$PREFIX}
  lcontext+=${lcontext:+ }${(pj:\0:)${(@b)words}}
  rcontext=${RBUFFER#$SUFFIX$ISUFFIX*${(~j:*:)words[CURRENT+1,-1]}}
}

${0}() {
  setopt localoptions completeinword NO_aliases

  private lbuffer="$LBUFFER" rbuffer="$RBUFFER"
  private keymap_menuselect="$( bindkey -M menuselect -L )"
  {
    bindkey -M menuselect -s '^R' '^_^_^R'
    bindkey -M menuselect -s '^S' '^_^_^S'
    bindkey -M menuselect '\e[D' .backward-char
    bindkey -M menuselect '\eOD' .backward-char
    bindkey -M menuselect '\e[C' .forward-char
    bindkey -M menuselect '\eOC' .forward-char

    local lcontext= rcontext=
    zle ${(%):-%N}.context
    LBUFFER="$lcontext"
    RBUFFER="$rcontext"

    [[ -o sharehistory ]] &&
        fc -RI  # Get new history events from file.
    zle _history_search

  } always {
    if [[ $BUFFER == $lcontext$rcontext ]] then
      LBUFFER="$lbuffer"
      RBUFFER="$rbuffer"
    fi
    bindkey -M menuselect -r '^R'
    bindkey -M menuselect -r '^S'
    bindkey -M menuselect -r '\e[D'
    bindkey -M menuselect -r '\eOD'
    bindkey -M menuselect -r '\e[C'
    bindkey -M menuselect -r '\eOC'
    eval "$keymap_menuselect"
  }
}

$0 "$@"
