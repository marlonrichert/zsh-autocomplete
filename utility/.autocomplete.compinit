#!/bin/zsh
emulate -L zsh -o noshortloops -o warncreateglobal -o extendedglob

# Workaround for https://github.com/zdharma/zinit/issues/366
[[ -v functions[.zinit-shade-off] ]] && .zinit-shade-off "${___mode:-load}"

unfunction compinit
autoload -Uz compinit
local -a desc_by_date=( ${${(%):-%x}:A:h:h}/**/*(DN.om) )
if [[ -z "${functions[_autocomplete.extras]}" || -z "$_comp_dumpfile" ||
    $desc_by_date[1] -nt "$_comp_dumpfile" ]]; then
  if [[ -e "$_comp_dumpfile" ]]; then
    command rm -f "$_comp_dumpfile"
    compinit -C -d "$_comp_dumpfile"
  else
    compinit -C
  fi
fi

_comp_options+=(
  globdots globstarshort numericglobsort
  NO_alwaystoend NO_caseglob NO_completealiases NO_completeinword
  NO_globcomplete NO_listbeep NO_listtypes NO_markdirs
)

.autocomplete.patch _main_complete
_main_complete() {
  eval "$_comp_setup"

  local word=$PREFIX$SUFFIX
  local _autocomplete__tail=${word##*/}
  local _autocomplete__head=${word%%$_autocomplete__tail}
  local _autocomplete__alnum=${_autocomplete__tail##[^[:alnum:]]##}
  local _autocomplete__punct=${_autocomplete__tail%%$_autocomplete__alnum}

  (( $#comppostfuncs == 0 )) &&
    local +h -a comppostfuncs=( _autocomplete.extras _autocomplete.long_list )
  compstate[insert]='automenu'
  .autocomplete._main_complete "$@"
}

.autocomplete.patch _default
_default() {
  setopt localoptions localtraps localpatterns ${_comp_options[@]}

  .autocomplete._default
  local ret=$?
  _autocomplete.history_words
  return ret || ?
}

.autocomplete.patch _description
_description() {
  setopt localoptions localtraps localpatterns ${_comp_options[@]}

  .autocomplete._description "$@"
  local ret=$?
  if [[ $3 == corrections ]]; then
    _lastdescr=( "${(@)_lastdescr[1,-2]}" )
  fi
  return ret
}

.autocomplete.patch _complete
_complete() {
  setopt localoptions localtraps localpatterns ${_comp_options[@]}

  local -i nmatches=$compstate[nmatches]
  .autocomplete._complete "$@"
  local ret=$?
  (( compstate[nmatches] == nmatches )) && _comp_mesg=''
  return ret
}

.autocomplete.patch _correct
_correct() {
  setopt localoptions localtraps localpatterns ${_comp_options[@]}

  if [[ -v compstate[quote] || $words[CURRENT] == [$histchars]* ]] || _autocomplete.is_glob; then
    return 1
  fi

  .autocomplete._correct "$@"
}

.autocomplete.patch _expand
_expand() {
  setopt localoptions localtraps localpatterns ${_comp_options[@]}

  if _autocomplete.is_glob && [[ $WIDGET != _complete_help ]]; then
    local ISUFFIX="*"
    {
      compadd() {
        setopt localoptions localtraps localpatterns
        setopt listtypes
        builtin compadd -f "$@"
      }
      .autocomplete._expand "$@"
      local ret=$?
    } always {
      unfunction compadd
    }
    return ret
  else
    .autocomplete._expand "$@"
  fi
}
