#!/bin/zsh
setopt localoptions extendedglob completeinword NO_listbeep

local -i index
local context lbuffer="$LBUFFER" rbuffer="$RBUFFER"

index=$LBUFFER[(I)$~_autocomplete__sublist_delim]
(( index < 1 )) ||
  context=$LBUFFER[1,index]
LBUFFER=${context:+$context }${LBUFFER[index+1,-1]//[[:space:]]##/$'\0'}

index=$RBUFFER[(i)$~_autocomplete__sublist_delim]
(( index > $#RBUFFER )) ||
  context=$RBUFFER[index,-1]
RBUFFER=${RBUFFER[1,index-1]//[[:space:]]##/$'\0'}${context:+ $context}

local searchbuffer="$BUFFER"

local -A keymap_historysearch=(
  $key[Left] .backward-char
  $key[Right] .forward-char
  $key[ForwardWord] .backward-word
  $key[BackwardWord] .forward-word
  $key[Home] .beginning-of-line
  $key[End] .end-of-line
)
local -i ret=1
{
  local keymap_menuselect="$(bindkey -M menuselect -L)"
  local k v; for k v in ${(kv@)keymap_historysearch}; do
    bindkey -M menuselect $k $v
  done

  zle _history_search; ret=$?

} always {
  eval "$keymap_menuselect"
  if [[ $BUFFER == $searchbuffer ]]; then
    LBUFFER="$lbuffer"
    RBUFFER="$rbuffer"
  fi
}
return ret
