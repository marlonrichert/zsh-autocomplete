#!/bin/zsh
builtin autoload -Uz .autocomplete.complete-word.post

local +h curcontext=${curcontext:-${WIDGET}:::}
local +h -a comppostfuncs=( .autocomplete.complete-word.post "$comppostfuncs[@]" )
if [[ $WIDGET == *menu-* && -v _autocomplete__partial_list ]] ||
    ( ( [[ "$KEYS" == "$key[Shift-Tab]" ]] ||
        zstyle -t ":autocomplete:${(kL)key[(Re)$KEYS]}:" insert-unambiguous
      ) && [[
          $_lastcomp[iprefix]$_lastcomp[prefix]$_lastcomp[suffix]$_lastcomp[isuffix] !=
              *${_lastcomp[unambiguous]:#?}*
      ]] ); then
  compstate[old_list]=
  _main_complete
elif [[ -n $compstate[old_list] ]]; then
  compstate[old_list]=keep
  [[ "$KEYS" == "$key[Shift-Tab]" && $_lastcomp[tags] == *all-matches ]] &&
      compstate[insert]=all
  _main_complete -
else
  _main_complete
fi
return 0  # Prevent beeping.
