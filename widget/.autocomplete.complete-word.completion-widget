#!/bin/zsh

.autocomplete.complete-word.completion-widget() {
  local +h -a comppostfuncs=( "$comppostfuncs[@]" .autocomplete.complete-word.comppostfunc )
  _main_complete
  unset curcontext
}

.autocomplete.complete-word.comppostfunc() {
  if zstyle -t :autocomplete:tab: insert-unambiguous && [[ $KEYS == $key[Tab] ]] &&
      (( compstate[nmatches] > 1 && $#compstate[unambiguous] > $#words[CURRENT] )); then
    compstate[insert]="${${(M)WIDGET:#menu-*}:+automenu-}unambiguous"
    return 0
  fi

  compstate[insert]="${${(M)WIDGET:#menu-*}:+menu:}"
  case $KEYS in
    $key[BackTab])
      compstate[insert]+='0'
      ;;
    *)
      compstate[insert]+='1'
      ;|
    $key[Tab])
      [[ $compstate[context] == command ]] &&
        compstate[insert]+=' '
      ;;
  esac
  (( _lastcomp[nmatches] > 0 ))
}

.autocomplete.complete-word.completion-widget "$@"
