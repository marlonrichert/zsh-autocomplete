#!/bin/zsh

.autocomplete.complete-word.completion-widget() {
  local +h -a comppostfuncs=( "$comppostfuncs[@]" .autocomplete.complete-word.comppostfunc )
  _main_complete
  unset curcontext
}

.autocomplete.complete-word.comppostfunc() {
  local keyname="${(kL)key[(Re)$KEYS]}"

  if zstyle -t ":autocomplete:${keyname}:" insert-unambiguous &&
      (( compstate[nmatches] > 1 && $#compstate[unambiguous] > $#words[CURRENT] )); then
    compstate[insert]="${${(M)WIDGET:#menu-*}:+automenu-}unambiguous"
    return 0
  fi

  local -i nmatches=$compstate[nmatches]
  [[ compstate[nmatches] -eq 0 && -n compstate[old_list] ]] &&
    nmatches=$_lastcomp[nmatches]

  compstate[insert]=1
  (( nmatches > 1 )) &&
    compstate[insert]="${${(M)WIDGET:#menu-*}:+menu:}${${${keyname:#backtab}:+1}:-0}"

  (( nmatches > 0 ))
}

.autocomplete.complete-word.completion-widget "$@"
